language ESSENCE' 1.0

letting v be 13
letting k be 3
letting l be 3
letting b be (l*v*(v-1))/(k*(k-1))  $ = 26
letting r be (l*(v-1))/(k-1)        $ = 6

find bibd: matrix indexed by [int(1..v), int(1..b)] of int(0..1)

such that
  $ each block contains k different objects 
  forAll block : int(1..b) .
    (sum object : int(1..v). bibd[object, block]) = k,

  $ each object occurs in r blocks
  forAll object : int(1..v) .
    (sum block : int(1..b). bibd[object, block]) = r,

  $ two different objects occur in a block exactly l times
  forAll object1 : int(1..v) .
    forAll object2 : int(1..v) .
      (object1 < object2) ->  
      ((sum block : int(1..b). 
        bibd[object1,block] * bibd[object2, block]) = l),

  $ Some symmetry breaking constraints
  forAll row: int(1..v-1) .
    bibd[row,..] <=lex bibd[row+1,..],

  forAll col: int(1..b-1) .
    bibd[..,col] <=lex bibd[..,col+1]
